plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
    id("io.freefair.lombok") version "8.13.1"
}

allure {
    report {
        version.set("2.21.0")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.21.0") //версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}

def selenideVersion = "7.5.1", //7.5.1
    restAssuredVersion = "5.5.5",
    junitVersion = "5.9.3",
    allureVersion = "2.21.0",
    ownerVersion = "1.0.12",
    slf4jVersion = "2.0.7",
    appiumVersion = "9.2.2"

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "io.appium:java-client:$appiumVersion"
    )
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task authorization_test(type: Test) {
    useJUnitPlatform {
        includeTags("authorization")
    }
}

task authorization_api_test(type: Test) {
    useJUnitPlatform {
        includeTags("authorization-api")
    }
}

task authorization_web_test(type: Test) {
    useJUnitPlatform {
        includeTags("authorization-web")
    }
}

task items_api_test(type: Test) {
    useJUnitPlatform {
        includeTags("items-api")
    }
}